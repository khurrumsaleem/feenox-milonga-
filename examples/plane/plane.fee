dt = 1e-5
end_time = 20

# problem parameters
h0 = 8           # plane height [miles]
vp = 600/3600    # plane speed [miles/second]
vm = 2000/3600   # missile speed [miles/second]

PHASE_SPACE xp yp theta xm ym s

# non-zero initial conditions
yp_0 = h0
theta_0 = pi/2

# DAE system
xp_dot .= vp
yp_dot .= 0
theta .= atan2(yp-ym, xp-xm)
xm_dot .= vm*cos(theta)
ym_dot .= vm*sin(theta)
s_dot .= sqrt(xm_dot^2+ym_dot^2)

# output
PRINT %.8f t xp yp xm ym theta s %e ((xm-xp)^2+(ym-yp)^2) 

# termination condition
done = ((xm-xp)^2+(ym-yp)^2)<1e-12

IF done
 PRINT "\#   distance the plane traveled: " xp "miles"
 PRINT "\#                time to impact: " t  "seconds"
 PRINT "\# distance the missile traveled: " s  "miles"
ENDIF 
