PROBLEM laplace 2D
static_steps = 20
READ_MESH wing.msh

# boundary conditions constant -> streamline
BC bottom  phi=0
BC top     phi=1

DEFAULT_ARGUMENT_VALUE 1 0.5
c_init = $1
BC wing    phi=c

SOLVE_PROBLEM

# velocities
PHYSICAL_GROUP left DIM 1
vx(x,y) = +dphidy(x,y) * left_length
vy(x,y) = -dphidx(x,y) * left_length

PHYSICAL_GROUP kutta DIM 0
error = phi(kutta_cog[1], kutta_cog[2]) - c
PRINT step_static %.10f c %.0e error
c = c + 10*error

# check for convergence
done_static = done_static | (abs(error) < 1e-8)
IF done_static
  WRITE_MESH $0-converged.msh phi VECTOR vx vy 0
  WRITE_MESH $0-converged.vtk phi VECTOR vx vy 0
  
  # Function unit tangent:
  PHYSICAL_GROUP circle DIM 1
  cx = circle_cog[1]
  cy = circle_cog[2]
  tx(x,y) = -(y-cy)/sqrt((x-cx)*(x-cx)+(y-cy)*(y-cy))
  ty(x,y) = +(x-cx)/sqrt((x-cx)*(x-cx)+(y-cy)*(y-cy))

  # Circulation on the circumference:
  INTEGRATE vx(x,y)*tx(x,y)+vy(x,y)*ty(x,y) OVER circle RESULT circ
  PRINT "circt: " circ

  INTEGRATE vx(x,y)*(+ny)+vy(x,y)*(-nx) OVER circle RESULT circ
  PRINT "circn: " circ
  
  INTEGRATE -vx(x,y) OVER top    GAUSS RESULT inttop
  INTEGRATE +vx(x,y) OVER bottom GAUSS RESULT intbottom
  INTEGRATE -vy(x,y) OVER left   GAUSS RESULT intleft
  INTEGRATE +vy(x,y) OVER right  GAUSS RESULT intright
  PRINT "box: " inttop+intleft+intbottom+intright
ENDIF
