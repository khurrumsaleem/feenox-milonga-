DEFAULT_ARGUMENT_VALUE 1 dirichlet # BCs = dirichlet/neumann
DEFAULT_ARGUMENT_VALUE 2 tet4      # shape = tri/quad
DEFAULT_ARGUMENT_VALUE 3 struct    # algorithm = struct/frontal/delaunay
DEFAULT_ARGUMENT_VALUE 4 4         # refinement factor = 1/2/3/4...
DEFAULT_ARGUMENT_VALUE 5 0         # write vtk? = 0/1

READ_MESH cube-$2-$3-$4.msh DIMENSIONS 3
PROBLEM thermal PC mumps

# MMS data, set T_mms(x) and k_mms(x) as desired
T_mms(x,y,z) := 1 + sin(2*x)^2 * cos(3*y)^2 * sin(4*z)
k_mms(x,y,z) := 1

# read the results of the symbolic derivatives
INCLUDE thermal-cube1-q.fee

# set the PDE coefficients and BCs
k(x,y,z) := k_mms(x,y,z)
q(x,y,z) := q_mms(x,y,z)

# set the BCs (depending on $1)
INCLUDE thermal-cube-bc-$1.fee

SOLVE_PROBLEM   # this line should be self-explanatory 

# output
IF $5

  WRITE_MESH thermal-cube1_$1-$2-$3-$4.vtk T T_mms T(x,y,z)-T_mms(x,y,z)

ELSE

  INTEGRATE 1 RESULT volume
  h = (volume/cells)^(1/3)

  INTEGRATE (T(x,y,z)-T_mms(x,y,z))^2 RESULT e_2
  error_2 = sqrt(e_2)

  FIND_EXTREMA abs(T(x,y,z)-T_mms(x,y,z)) MAX error_inf

  PRINT %e h error_inf error_2 log(h) log(error_inf) log(error_2) %g $4 cells nodes %.3f memory() %.2f wall_time()

ENDIF
