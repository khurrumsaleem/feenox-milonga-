PROBLEM mechanical 3D
READ_MESH nafems-le10.msh

BC upper    p=1
BC DCD'C'   v=0
BC ABA'B'   u=0
BC BCB'C'   u=0 v=0
BC midplane w=0

E = 210e3
nu = 0.3

SOLVE_PROBLEM 

MATRIX stress ROWS 3 COLS 3 DATA \
    sigmax(2000,0,300)  tauxy(2000,0,300)  tauzx(2000,0,300) \
    tauxy(2000,0,300)  sigmay(2000,0,300)  tauyz(2000,0,300) \
    tauzx(2000,0,300)   tauyz(2000,0,300) sigmaz(2000,0,300)
    
INTEGRATE 1 RESULT vol

INTEGRATE u RESULT Iu
INTEGRATE v RESULT Iv
INTEGRATE w RESULT Iw
FIND_EXTREMA u(x,y,z)^2+v(x,y,z)^2+w(x,y,z)^2 X_MAX displ_x Y_MAX displ_y Z_MAX displ_z

INTEGRATE sigma RESULT Is
FIND_EXTREMA sigma MIN min_sigma X_MIN min_sigma_x Y_MIN min_sigma_y Z_MIN min_sigma_z \
                   MAX max_sigma X_MAX max_sigma_x Y_MAX max_sigma_y Z_MAX max_sigma_z
                   
PRINT %+.2f stress
PRINT %.2f sigma(2000,0,300)

PRINT %+.4f Iu/vol Iv/vol Iw/vol
PRINT %+.4f u(displ_x,displ_y,displ_z) v(displ_x,displ_y,displ_z) w(displ_x,displ_y,displ_z)
PRINT %+.2f displ_x displ_y displ_z
PRINT %+.2f min_sigma max_sigma Is/vol
